<Program>      --> BRGINOROTEST PROGNAME ENDLINE <Code> ENDPROG ENDLINE
<Code>         --> <Instruction> ENDLINE <Code> 
               --> epsilon 
<Instruction>  --> <Assign> 
               --> <If>
               --> <While>
               --> <Print>
               --> <Read>
<Assign>       --> VARNAME ASSIGN <ExprArith> 
<ExprArith>    --> <ProdExpr> <ExprTail>
<ExprTail>     --> <AddOp> <ProdExpr> <ExprTail>
               --> epsilon
<ProdExpr>     --> <ExprAtom> <ProdTail> 
<ProdTail>     --> <MultOp> <ExprAtom> <ProdTail>
               --> epsilon
<ExprAtom>     --> MINUS <ExprAtom>
               --> VARNAME
               --> NUMBER
               --> LPAREN <ExprArith> RPAREN
<AddOp>        --> PLUS
               --> MINUS
<MultOp>       --> TIMES
               --> DIVIDE
<Cond>         --> <ExprArith> <Comp> <ExprArith>
<If>           --> IF LPAREN <Cond> RPAREN THEN ENDLINE <Code> <MaybeElse> ENDIF 
<MaybeElse>    --> ELSE ENDLINE <Code> 
               --> epsilon
<Comp>         --> EQ
               --> GT
<While>        --> WHILE LPAREN <Cond> RPAREN DO ENDLINE <Code> ENDWHILE
<Print>        --> PRINT LPAREN VARNAME RPAREN
<Read>         --> READ LPAREN VARNAME RPAREN
